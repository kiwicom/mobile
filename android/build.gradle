buildscript {
    apply from: "$rootDir/dependencies.gradle"
    apply from: "$rootDir/deployment.gradle"
    apply from: "$rootDir/rnkiwimobile/versions.gradle"

    repositories {
        mavenLocal()
        google()
        jcenter()
        reactNativeFromUnpkg(it, reactNativeVersion)
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "digital.wup:android-maven-publish:$androidMavenPublishVersion"
    }
}

subprojects {
    buildscript {
        apply from: "$rootDir/deployment.gradle"

        repositories {
            mavenLocal()
            google()
            jcenter()
            fabric(it)
            kiwi(it)
            reactNativeFromUnpkg(it, reactNativeVersion)
        }

        dependencies {
            classpath "com.android.tools.build:gradle:3.4.0-alpha04"
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            classpath "digital.wup:android-maven-publish:$androidMavenPublishVersion"
        }
    }

    apply plugin: 'digital.wup.android-maven-publish'

    repositories {
        mavenLocal()
        google()
        jcenter()
        fabric(it)
        kiwi(it)
        reactNativeFromUnpkg(it, reactNativeVersion)
    }
}

// Override configuration for external sub-projects.
allprojects {
    buildDir = "$rootDir/${project.name}/build"
    repositories {
        google()
    }
    afterEvaluate {
        def androidConfiguration = {
            android {
                compileSdkVersion = rootProject.ext.compileSdkVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }

                lintOptions {
                    abortOnError false
                }
            }
        }

        pluginManager.withPlugin("com.android.library", androidConfiguration)
        pluginManager.withPlugin("com.android.application", androidConfiguration)
    }

    switch (project.name) {
        case 'react-native':
            configure(project) {
                publishAndroidLibrary("com.facebook.react", project.name, "$reactNativeVersion")
            }
            break
        case 'react-native-code-push':
            configure(project) {
                publishAndroidLibrary("com.trinerdis.skypicker", project.name, "$reactNativeCodePush-SNAPSHOT")
            }
            break
        case 'react-native-maps':
            configure(project) {
                publishCustomLibrary("com.trinerdis.skypicker", project.name, "$reactNativeMaps-SNAPSHOT") {
                    it.artifact("$buildDir/outputs/aar/${project.name}-release.aar")
                }
            }
            break
        case 'react-native-gesture-handler':
            configure(project) {
                publishAndroidLibrary("com.trinerdis.skypicker", project.name, "$reactNativeGestureHandler-SNAPSHOT")
            }
            break
        case '@react-native-community_async-storage':
            configure(project) {
                publishAndroidLibrary("com.trinerdis.skypicker", project.name, "$reactNativeAsyncStorage-SNAPSHOT")
            }
            break
        case '@react-native-community_netinfo':
            configure(project) {
                publishAndroidLibrary("com.trinerdis.skypicker", project.name, "$reactNativeNetInfo-SNAPSHOT")
            }
            break
        case 'react-native-webview':
            configure(project) {
                publishAndroidLibrary("com.trinerdis.skypicker", project.name, "$reactNativeWebView-SNAPSHOT")
            }
            break
    }
}
