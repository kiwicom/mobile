import groovy.json.JsonSlurper

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "../dependencies.gradle"

def inputFile = new File("${rootDir}/../.build/package.json")
def packageJson = new JsonSlurper().parseText(inputFile.text)

def getPassword(String currentUser, String keyChain) {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    exec {
        commandLine 'security', '-q', 'find-generic-password', '-a', currentUser, '-s', keyChain, '-w'
        standardOutput = stdout
        errorOutput = stderr
        ignoreExitValue true
    }
    //noinspection GroovyAssignabilityCheck
    stdout.toString().trim()
}

def stageCodePushKey = getPassword("rnkiwimobile-codepush", "staging")
def productionCodePushKey = getPassword("rnkiwimobile-codepush", "production")

android {
    compileSdkVersion compileSdkVersionVar
    defaultConfig {
        applicationId "com.kiwi.mobile.rnandroidplayground"
        minSdkVersion minSdkVersionVar
        targetSdkVersion targetSdkVersionVar
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    project.ext.rnKiwiMobileVersion = packageJson['version']

    signingConfigs {
        stagingRelease {
            // Just a fake keystore / fake password
            storeFile file("playground-key.keystore")
            storePassword 'kiwireactnative'
            keyAlias 'my-key-alias'
            keyPassword 'kiwireactnative'
        }
        release {
            // Just a fake keystore / fake password
            storeFile file("playground-key.keystore")
            storePassword 'kiwireactnative'
            keyAlias 'my-key-alias'
            keyPassword 'kiwireactnative'
        }
    }
    buildTypes {
        debug {
            // Note: CodePush updates should not be tested in Debug mode as they are overriden by the RN packager.
            // However, because CodePush checks for updates in all modes, we must supply a key.
            buildConfigField "String", "CODEPUSH_KEY", '""'
        }
        stagingRelease {
            buildConfigField "String", "CODEPUSH_KEY", "\"${stageCodePushKey}\""
            buildConfigField "String", "CODEPUSH_VERSION", "\"1.0.0\""
            // Note: It is a good idea to provide matchingFallbacks for the new buildType you create to prevent build issues
            // Add the following line if not already there
            matchingFallbacks = ['release']
            signingConfig signingConfigs.stagingRelease
        }
        release {
            buildConfigField "String", "CODEPUSH_KEY", "\"${productionCodePushKey}\""
            buildConfigField "String", "CODEPUSH_VERSION", "\"1.0.0\""
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /**
     * In debug mode, it will use the local rnkiwimobile project.
     * If we don't have anything in .build/android, then it will also use the packager
     *
     * In release mode, it will use the rnkiwimobile from Trinerdis (without packager running)
     */
    implementation project(':rnkiwimobile')
    stagingReleaseImplementation project(':rnkiwimobile')
    releaseImplementation "com.trinerdis.skypicker:rnkiwimobile:$rnKiwiMobileVersion-SNAPSHOT"

    implementation "com.trinerdis.skypicker:logging:$loggingVersion-SNAPSHOT"

    implementation "com.android.support:multidex:1.0.3"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
