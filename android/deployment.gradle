def gitLabRepositoryUrl = "https://gitlab.skypicker.com/api/v4/projects/301/packages/maven"

ext.packageFromNodeModules = { name ->
    it.maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/$name/android")
    }
}

ext.packageFromUnpkg = { it, name, version ->
    it.maven {
        url "https://unpkg.com/$name@$version/android/"
    }
}

ext.fabric = {
    it.maven {
        url 'https://maven.fabric.io/public'
    }
}

ext.kiwi = {
    it.maven {
        url = gitLabRepositoryUrl
        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = System.getenv("RNKIWIMOBILE_DEPLOYMENT_TOKEN")
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

ext.publishCustomLibrary = { _groupId, _artifactId, _version, closure ->
    apply plugin: 'digital.wup.android-maven-publish'

    publishing {
        repositories {
            kiwi(it)
        }

        publications {
            maven(MavenPublication) {
                groupId = _groupId
                artifactId = _artifactId.replaceAll("@", "")
                version = _version

                closure(it)
            }
        }
    }

    publish {
        dependsOn 'assemble'
    }

    tasks.create('uploadKiwi') {
        dependsOn 'publish'
    }
}

ext.publishJavaLibrary = { groupId, artifactId, version ->
    publishCustomLibrary(groupId, artifactId, version) {
        it.from components.java
    }
}

ext.publishAndroidLibrary = { groupId, artifactId, version ->
    apply plugin: "com.android.library"

    publishCustomLibrary(groupId, artifactId, version) {
        it.from components.android
    }
}
